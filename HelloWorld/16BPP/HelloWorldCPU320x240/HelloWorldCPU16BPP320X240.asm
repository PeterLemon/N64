// N64 'Bare Metal' 16BPP 320x240 Hello World Demo by krom (Peter Lemon):
arch n64.cpu
endian msb
output "HelloWorldCPU16BPP320X240.N64", create
fill 1052672 // Set ROM Size

// Setup Frame Buffer
constant SCREEN_X(320)
constant SCREEN_Y(240)
constant BYTES_PER_PIXEL(2)

// Setup Characters
constant CHAR_X(8)
constant CHAR_Y(8)

origin $00000000
base $80000000 // Entry Point Of Code
include "LIB/N64.INC" // Include N64 Definitions
include "LIB/N64_HEADER.ASM" // Include 64 Byte Header & Vector Table
insert "LIB/N64_BOOTCODE.BIN" // Include 4032 Byte Boot Code

macro PrintString(vram, xpos, ypos, fontfile, string, length) { // Print Text String To VRAM Using Font At X,Y Position
  li a0,{vram}+({xpos}*BYTES_PER_PIXEL)+(SCREEN_X*BYTES_PER_PIXEL*{ypos}) // A0 = Frame Buffer Pointer (Place text at XY Position)
  la a1,{fontfile} // A1 = Font Address
  la a2,{string} // A2 = Text Address
  ori t0,r0,{length} // T0 = Number of Text Characters to Print
  {#}DrawChars:
    ori t1,r0,CHAR_X-1 // T1 = Character X Pixel Counter
    ori t2,r0,CHAR_Y-1 // T2 = Character Y Pixel Counter

    lbu t3,0(a2) // T3 = Next Text Character
    addiu a2,1 // Text Address++

    sll t3,7 // T3 *= 128 (Shift to Correct Position in Font)
    addu t3,a1 // T3 += Font Address

    {#}DrawCharX:
      lw t4,0(t3) // Load Font Text Character Pixel
      addiu t3,BYTES_PER_PIXEL
      sw t4,0(a0) // Store Font Text Character Pixel into Frame Buffer
      addiu a0,BYTES_PER_PIXEL

      bnez t1,{#}DrawCharX // IF (Character X Pixel Counter != 0) DrawCharX
      subiu t1,1 // Decrement Character X Pixel Counter (Delay Slot)

      addiu a0,(SCREEN_X*BYTES_PER_PIXEL)-CHAR_X*BYTES_PER_PIXEL // Jump Down 1 Scanline, Jump Back 1 Char
      ori t1,r0,CHAR_X-1 // Reset Character X Pixel Counter
      bnez t2,{#}DrawCharX // IF (Character Y Pixel Counter != 0) DrawCharX
      subiu t2,1 // Decrement Character Y Pixel Counter (Delay Slot)

    subiu a0,((SCREEN_X*BYTES_PER_PIXEL)*CHAR_Y)-CHAR_X*BYTES_PER_PIXEL // Jump To Start Of Next Char
    bnez t0,{#}DrawChars // Continue to Print Characters
    subiu t0,1 // Subtract Number of Text Characters to Print (Delay Slot)
}

Start:
  include "LIB/N64_GFX.INC" // Include Graphics Macros
  N64_INIT() // Run N64 Initialisation Routine

  ScreenNTSC(320, 240, BPP16, $A0100000) // Screen NTSC: 320x240, 16BPP, DRAM Origin = $A0100000

  PrintString($A0100000, 128, 32, FontBlack, Text, 11) // Print Text String To VRAM Using Font At X,Y Position
  PrintString($A0100000, 192, 96, FontRed, Text, 11) // Print Text String To VRAM Using Font At X,Y Position

Loop:
  j Loop
  nop // Delay Slot

Text:
  db "Hello World!"

align(4) // Align 32-Bit
insert FontBlack, "FontBlack8x8.bin"
insert FontRed, "FontRed8x8.bin"